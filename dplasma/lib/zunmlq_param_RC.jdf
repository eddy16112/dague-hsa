extern "C" %{
/*
 * Copyright (c) 2010-2013 The University of Tennessee and The University
 *                         of Tennessee Research Foundation. All rights
 *                         reserved.
 * Copyright (c) 2013      Inria. All rights reserved.
 * $COPYRIGHT
 *
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasmajdf.h"
#include "data_dist/matrix/matrix.h"

%}

side      [type = "PLASMA_enum"]
trans     [type = "PLASMA_enum"]
dataA     [type = "dague_ddesc_t *"]
dataB     [type = "dague_ddesc_t *"]
dataTS    [type = "dague_ddesc_t *" aligned=dataA ]
dataTT    [type = "dague_ddesc_t *" aligned=dataA ]
qrtree    [type = "dplasma_qrtree_t"]

descA   [type = "tiled_matrix_desc_t" default="*((tiled_matrix_desc_t*)dataA)"  hidden=on]
descB   [type = "tiled_matrix_desc_t" default="*((tiled_matrix_desc_t*)dataB)"  hidden=on]
descTS  [type = "tiled_matrix_desc_t" default="*((tiled_matrix_desc_t*)dataTS)" hidden=on]
descTT  [type = "tiled_matrix_desc_t" default="*((tiled_matrix_desc_t*)dataTT)" hidden=on]
ib      [type = "int" hidden = on default = "descTS.mb" ]
KT      [type = "int" hidden = on default = "dplasma_imin(descA.nt-2, descA.mt-1)" ]
p_work  [type = "dague_memory_pool_t *" size = "(sizeof(PLASMA_Complex64_t)*ib*(descTS.nb))"]

zunmlq(k, i, m)
  /* Execution space */
  k = 0 .. descA.mt-1
  i = 0 .. inline_c %{ return qrtree.getnbgeqrf( &qrtree, k ) - 1; %}
  m = 0 .. descB.mt-1

  p     = inline_c %{ return qrtree.getm(    &qrtree, k, i); %}
  nextp = inline_c %{ return qrtree.nextpiv( &qrtree, k, p, descA.nt); %}

  /* Locality */
  : dataB(m, p)

  READ  A    <- A zunmlq_in(k, i)   [type = UPPER_TILE]
  READ  T    <- T zunmlq_in(k, i)   [type = LITTLE_T]
  RW    C    <- ( k == 0 ) ? dataB(m, p)
             <- ( k >  0 ) ? A2 zttmlq(k-1, m, p)
             -> ( k == descA.nt-1 ) ? dataB(m, p)
             -> ((k <  descA.nt-1) & (nextp != descA.nt) ) ? A1 zttmlq(k, m, nextp)
             -> ((k <  descA.nt-1) & (nextp == descA.nt) ) ? A2 zttmlq(k, m, p    )

BODY
{
    int tempmm = (m == (descB.mt-1)) ? (descB.m - m * descB.mb) : descB.mb;
    int temppn = (p == (descB.nt-1)) ? (descB.n - p * descB.nb) : descB.nb;
    int tempkm = (k == (descA.mt-1)) ? (descA.m - k * descA.mb) : descA.mb;
    int tempmin = dplasma_imin( tempkm, temppn );
    int ldak    = BLKLDD( descA, k );
    int ldbm    = BLKLDD( descB, m );

    void *W = dague_private_memory_pop( p_work );

    printlog("CORE_zunmlq(%d, %d, %d) [%d, %d]\n"
             "\t(side=%s, trans=%s, M=%d, N=%d, K=%d, ib=%d,\n"
             "\t A(%d,%d)[%p], lda=%d, T(%d,%d)[%p], ldt=%d, B(%d,%d)[%p], ldb=%d, W=%p, LDW=%d)\n",
             k, i, m, p, nextp, plasma_const( side ), plasma_const( trans ), tempmm, temppn, tempmin, ib,
             k, p, A, ldak, k, p, T, descTS.mb, m, p, C, ldbm, W, descTS.nb );

#if !defined(DAGUE_DRY_RUN)
    CORE_zunmlq(
        side, trans,
        tempmm, temppn, tempmin, ib,
        A /* A(k, p) */, ldak,
        T /* T(k, p) */, descTS.mb,
        C /* B(m, p) */, ldbm,
        W, descTS.nb );
#endif /* !defined(DAGUE_DRY_RUN) */

    dague_private_memory_push( p_work, W );
}
END

zunmlq_in(k,i)  [profile = off]
  k = 0 .. descA.mt-1
  i = 0 .. inline_c %{ return qrtree.getnbgeqrf( &qrtree, k ) - 1; %}
  p = inline_c %{ return qrtree.getm( &qrtree, k, i); %}

  : dataA(k, p)

  RW A <- dataA(k, p)                      [type = UPPER_TILE]
       -> A zunmlq(k, i, 0 .. descB.mt-1)  [type = UPPER_TILE]

  RW T <- dataTS(k, p)                     [type = LITTLE_T]
       -> T zunmlq(k, i, 0 .. descB.mt-1)  [type = LITTLE_T]

BODY
{
    /* nothing */
}
END

zttmlq(k, m, n)
  /* Execution Space */
  k = 0   .. KT
  m = 0   .. descB.mt-1
  n = k+1 .. descB.nt-1

  p =     inline_c %{ return qrtree.currpiv( &qrtree, k,   n);    %}
  nextp = inline_c %{ return qrtree.nextpiv( &qrtree, k,   p, n); %}
  prevp = inline_c %{ return qrtree.prevpiv( &qrtree, k,   p, n); %}
  prevn = inline_c %{ return qrtree.prevpiv( &qrtree, k,   n, n); %}
  type  = inline_c %{ return qrtree.gettype( &qrtree, k,   n );   %}
  type1 = inline_c %{ return qrtree.gettype( &qrtree, k+1, n );   %}
  ip    = inline_c %{ return qrtree.geti(    &qrtree, k,   p );   %}
  in    = inline_c %{ return qrtree.geti(    &qrtree, k,   n );   %}
  in1   = inline_c %{ return qrtree.geti(    &qrtree, k+1, n );   %}

  /* Locality */
  : dataB(m, n)

  RW    A1   <-  (prevp == descA.nt) ? C zunmlq( k, ip, m ) : A1 zttmlq(k, m, prevp )

             -> ((nextp == descA.nt) & ( p == k ) ) ? A  zttmlq_out_B(p, m)
             -> ((nextp == descA.nt) & ( p != k ) ) ? A2 zttmlq( k, m, p    )
             ->  (nextp != descA.nt) ?                A1 zttmlq( k, m, nextp)

  RW    A2   <- ( (type  == 0 ) && (k     == 0        ) ) ? dataB(m, n)
             <- ( (type  == 0 ) && (k     != 0        ) ) ? A2 zttmlq(k-1, m, n )
             <- ( (type  != 0 ) && (prevn == descA.nt ) ) ? C  zunmlq(k, in, m    )
             <- ( (type  != 0 ) && (prevn != descA.nt ) ) ? A1 zttmlq(k, m, prevn )

             ->   (k == (descA.mt-1))                    ? dataB( m, n )
             -> ( (k <  (descA.mt-1)) && (type1 != 0 ) ) ? C  zunmlq( k+1, in1, m )
             -> ( (k <  (descA.mt-1)) && (type1 == 0 ) ) ? A2 zttmlq( k+1, m, n )

  READ  V    <- (type == 0) ? A zttmlq_in(k,n)
             <- (type != 0) ? A zttmlq_in(k,n)                       [type = LOWER_TILE]

  READ  T    <- T zttmlq_in(k,n)                                     [type = LITTLE_T]

BODY
{
    int tempmm = ( m == (descB.mt-1)) ? (descB.m - m * descB.mb) : descB.mb;
    int tempnn = ( n == (descB.nt-1)) ? (descB.n - n * descB.nb) : descB.nb;
    int tempkm = ( k == (descA.mt-1)) ? (descA.m - k * descA.mb) : descA.mb;
    int ldak = BLKLDD( descA, k );
    int ldbm = BLKLDD( descB, m );
    int ldwork = descTS.nb;

    void *W = dague_private_memory_pop( p_work );

    printlog("CORE_ztsmlq(%d, %d, %d)\n"
             "\t(side=%s, trans=%s, M1=%d, N1=%d, M2=%d, N2=%d, K=%d, ib=%d,\n"
             "\t A1(%d,%d)[%p], lda1=%d, A2(%d,%d)[%p], lda2=%d, V(%d,%d)[%p], ldv=%d, T(%d,%d)[%p], ldt=%d, W=%p, LDW=%d)\n",
             k, m, n, plasma_const( side ), plasma_const( trans ),
             tempmm, descB.nb, tempmm, tempnn, tempkm, ib,
             m, p, A1, ldbm, m, n, A2, ldbm, k, n, V, ldak, k, n, T, descTS.mb, W, ldwork );

#if !defined(DAGUE_DRY_RUN)
    if ( type == DPLASMA_QR_KILLED_BY_TS ) {
        CORE_ztsmlq(
            side, trans,
            tempmm, descB.nb, tempmm, tempnn, tempkm, ib,
            A1 /* B(m, p) */, ldbm,
            A2 /* B(m, n) */, ldbm,
            V  /* A(k, n) */, ldak,
            T  /* T(k, n) */, descTT.mb,
            W, ldwork );
    } else {
        CORE_zttmlq(
            side, trans,
            tempmm, descB.nb, tempmm, tempnn, tempkm, ib,
            A1 /* B(m, p) */, ldbm,
            A2 /* B(m, n) */, ldbm,
            V  /* A(k, n) */, ldak,
            T  /* T(k, n) */, descTT.mb,
            W, ldwork );
    }
#endif /* !defined(DAGUE_DRY_RUN) */

    dague_private_memory_push( p_work, W );
}
END

zttmlq_in(k, n)  [profile = off]
  k = 0   .. KT
  n = k+1 .. descA.nt-1
  type = inline_c %{ return qrtree.gettype( &qrtree, k, n );   %}

  : dataA(k, n)

  RW A <- dataA(k, n)
       -> (type == 0) ? V zttmlq(k, 0..descB.mt-1, n)
       -> (type != 0) ? V zttmlq(k, 0..descB.mt-1, n) [type = LOWER_TILE]

  RW T <- dataTT(k, n)                                [type = LITTLE_T]
       -> T zttmlq(k, 0..descB.mt-1, n)               [type = LITTLE_T]

BODY
{
    /* nothing */
}
END

zttmlq_out_B(k, m) [profile = off]
  k = 0 .. KT
  m = 0 .. descB.mt-1
  prevp = inline_c %{ return qrtree.prevpiv( &qrtree, k, k, k ); %}

  : dataB(m, k)

  RW A <- A1 zttmlq( k, m, prevp )
       -> dataB(m, k)

BODY
{
    /* nothing */
}
END

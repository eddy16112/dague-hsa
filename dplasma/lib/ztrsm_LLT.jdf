extern "C" %{
  /**
   * PLASMA include for defined and constants.
   *
   * @precisions normal z -> s d c
   *
   */
  
#include <plasma.h>
#include <core_blas.h>

#include "dague.h"
#include "data_distribution.h"
#include "dplasmajdf.h"

%}

/* Globals
 * GRIDrows, GRIDcols, NB, SIZE
 */
Bmb  [int]
Bnb  [int]
Bmt  [int]
Bnt  [int]
Bm   [int]
Bn   [int]
     
Amb  [int]
Anb  [int]
Amt  [int]
Ant  [int]
Am   [int]
An   [int]

trans[int]
diag [int]

alpha[Dague_Complex64_t]

/**************************************************
 *                      TRSM                      *
 **************************************************/
TRSM(k, n)

// Execution space
k = 0..Bmt-1
n = 0..Bnt-1

// Parallel partitioning
: B(k,n)

// Parameters
READ  A <- A(k,k)
RW    B <- (k == Bmt-1) ? B(0, n) : C GEMM(k-1, k, n)
        -> B GEMM(k, 0..k-1, n)
        -> B(k, n)

BODY
      Dague_Complex64_t lalpha = k == Bmt-1 ? alpha : (Dague_Complex64_t)1.0;
      DRYRUN(
	   CORE_ztrsm(
			PlasmaLeft, PlasmaLower, trans, diag,
			k == Bmt-1 ? Bm - k * Bmb : Bmb,
			n == Bnt-1 ? Bn - n * Bnb : Bnb,
			lalpha, A /*A(k, k)*/, Amb,
                                B /*B(k, n)*/, Bmb)
	   );

      OUTPUT( ("thread %d CORE_dtrsm( %s, %s, %s, %s, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d)\n",
	       context->eu_id, "PlasmaRight", "PlasmaLower", "Trans/ConjTrans", "PlasmaNonUnit",
	       k == Bmt-1 ? Bm - k * Bmb : Bmb,
	       n == Bnt-1 ? Bn - n * Bnb : Bnb,
	       lalpha, k, k, A /*A(k, k)*/, Amb,
	               k, n, B /*B(k, n)*/, Bmb)
	      );
END

/**************************************************
 *                      GEMM                      *
 **************************************************/
// Name
GEMM(k, m, n)

// Execution space
k = 0..Bmt-1
m = 0..k-1
n = 0..Bnt-1

// Parallel partitioning
: B(m, n)

// Parameters
READ  A <- A(k, m)
READ  B <- B TRSM(k, n)
RW    C <- (k == Bmt-1) ? B(m, n) : C GEMM(k+1, m, n)
        -> (k-1 == m)   ? B TRSM(m, n) : C GEMM(k-1, m, n)

BODY
      Dague_Complex64_t lalpha = k == 0 ? alpha : (Dague_Complex64_t)1.0;    
      DRYRUN(
	   CORE_zgemm(
			trans, PlasmaNoTrans,
			m == Bmt-1 ? Bm-m*Bmb : Bmb,
			n == Bnt-1 ? Bn-n*Bnb : Bnb,
			Bmb,
			(Dague_Complex64_t)-1.0, A /* A(k, m)*/ , Amb,
			              B /* B(k, n)*/ , Bmb,
			lalpha,       C /* B(m, n)*/ , Bmb)
	   );

      OUTPUT( ("thread %d CORE_dgemm( %s, %s, %d, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d, %f, B(%d,%d)[%p], %d)\n",
	       context->eu_id, "Trans/ConjTrans", "PlasmaNoTrans", 
	       m == Bmt-1 ? Bm-m*Bmb : Bmb,
	       n == Bnt-1 ? Bn-n*Bnb : Bnb,
	       Bmb,
	       (Dague_Complex64_t)-1.0, m, k, A, Amb,
                             k, n, B, Bmb,
	       lalpha,       m, n, C, Bmb)
	      );
END

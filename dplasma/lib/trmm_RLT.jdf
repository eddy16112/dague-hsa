extern "C" %{
  /**
   * PLASMA include for defined and constants.
   */
  
#include <plasma.h>
#include <core_blas.h>
#include "dague.h"
#include "remote_dep.h"
#include "data_dist/data_distribution.h"
#include "dplasmatypes.h"

%}

/* Globals
 * GRIDrows, GRIDcols, NB, SIZE
 */
Amb  [int]
Anb  [int]
Amt  [int]
Ant  [int]
Am   [int]
An   [int]

Bmb  [int]
Bnb  [int]
Bmt  [int]
Bnt  [int]
Bm   [int]
Bn   [int]
     
side [int]
uplo [int]
trans[int]
diag [int]

alpha[TYPENAME]

/**************************************************
 *                      TRMM                      *
 **************************************************/
TRMM(m, n)

// Execution space
m = 0..Bmt-1
n = Bnt-1..0

// Parallel partitioning
: B(m, n)

// Parameters
READ  A <- A(n, n)
RW    B <- B(m, n)
        -> (n == Bnt-1) ? B(m, n) : C GEMM(m, n, 0)

BODY
      CORE(
	   trmm, (
		  side, uplo, trans, diag,
		  m == Bmt-1 ? Bm - m * Bmb : Bmb,
		  n == Bnt-1 ? Bn - n * Bnb : Bnb,
		  alpha, A /*A(k, k)*/, Amb,
		         B /*B(k, n)*/, Bmb)
	   );

      OUTPUT( ("thread %d CORE_dtrsm( %s, %s, %s, %s, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d)\n",
	       context->eu_id, "PlasmaRight", "PlasmaUpper", "Trans/ConjTrans", "PlasmaNonUnit",
	       k == Bmt-1 ? Bm - k * Bmb : Bmb,
	       n == Bnt-1 ? Bn - n * Bnb : Bnb,
	       lalpha, k, k, A /*A(k, k)*/, Amb,
	               k, n, B /*B(k, n)*/, Bmb)
	      );
END

/**************************************************
 *                      GEMM                      *
 **************************************************/
// Name
GEMM(m, n, k)

// Execution space
m = 0..Bmt-1
n = Bnt-1..0
k = 0..n-1

// Parallel partitioning
: B(m, n)

// Parameters
READ  A <- B(m, k)
READ  B <- A(n, k) 
RW    C <- (k == 0)   ? B TRMM(m, n) : C GEMM(m, n, k-1)
        -> (k == n-1) ? B(m, n)      : C GEMM(m, n, k+1)

BODY
      CORE(
	   gemm, (
		  trans, PlasmaNoTrans,
		  m == Bmt-1 ? Bm-m*Bmb : Bmb,
		  n == Bnt-1 ? Bn-n*Bnb : Bnb,
		  Bmb,
		  alpha, A, Bmb,
		         B, Amb,
		  (TYPENAME)1.0,  C, Bmb)
	   );

      OUTPUT( ("thread %d CORE_dgemm( %s, %s, %d, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d, %f, B(%d,%d)[%p], %d)\n",
	       context->eu_id, "Trans/ConjTrans", "PlasmaNoTrans", 
	       m == Bmt-1 ? Bm-m*Bmb : Bmb,
	       n == Bnt-1 ? Bn-n*Bnb : Bnb,
	       Bmb,
	       (TYPENAME)-1.0, k, m, A, Amb,
                             k, n, B, Bmb,
	       lalpha,       m, n, C, Bmb)
	      );
END

extern "C" %{
/*
 *  Copyright (c) 2010
 *
 *  The University of Tennessee and The University
 *  of Tennessee Research Foundation.  All rights
 *  reserved.
 *
 * @precisions normal z -> z c d s
 *
 */
#define PRECISION_z

#include <core_blas.h>
#include <core_blas.h>

#include "dague.h"
#include "data_distribution.h"
#include "data_dist/matrix/precision.h"
#include "data_dist/matrix/matrix.h"
#include "dplasma/lib/memory_pool.h"
#include "dplasma/lib/dplasmajdf.h"

%}

dataA     [type = "dague_ddesc_t *"]
descA     [type = "tiled_matrix_desc_t" hidden = on default = "*((tiled_matrix_desc_t*)dataA)"]
dataT     [type = "dague_ddesc_t *"]
descT     [type = "tiled_matrix_desc_t" hidden = on default = "*((tiled_matrix_desc_t*)dataT)"]
dataQ     [type = "dague_ddesc_t *"]
descQ     [type = "tiled_matrix_desc_t" hidden = on default = "*((tiled_matrix_desc_t*)dataQ)"]
ib        [type = "int" hidden = on default = "descT.mb" ]
p_work    [type = "dague_memory_pool_t *" size = "((sizeof(dague_complex64_t))*ib)*descT.nb"]


zunmqr(k, n)
  /* Execution Space */
  k = 0 .. descA.nt-1
  n = 0 .. descQ.nt-1

  /* Locality */
  : dataQ(k,n)

  READ  A    <- A zunmqr_in_data_A0(k)   [type = LOWER_TILE]
  READ  T    <- T zunmqr_in_data_T1(k)   [type = LITTLE_T]
  RW    C    -> ( k == 0 ) ? dataQ(k,n)
             -> ( k >  0 ) ? A2 ztsmqr(k-1, k, n)
             <- ( k <  (descQ.mt-1)) ? A1 ztsmqr(k, k+1, n)
             <- ( k == (descQ.mt-1)) ? dataQ(k,n)

BODY
{
    int tempAkm  = (k == (descA.mt-1)) ? (descA.m - k * descA.mb) : descA.mb;
    int tempAkn  = (k == (descA.nt-1)) ? (descA.n - k * descA.nb) : descA.nb;
    int tempkmin = dague_imin( tempAkm, tempAkn );
    int tempkm   = (k == (descQ.mt-1)) ? (descQ.m - k * descQ.mb) : descQ.mb;
    int tempnn   = (n == (descQ.nt-1)) ? (descQ.n - n * descQ.nb) : descQ.nb;
    int ldak = BLKLDD( descA, k );
    int ldqk = BLKLDD( descQ, k );

    DRYRUN(
        void *p_elem_A = dague_private_memory_pop( p_work );

    fprintf(stderr,
            "CORE_zunmqr[%d,%d](Left, NoTrans, %d, %d, %d, %d\n"
            "            %p, %d, %p %d, %p, %d %p, %d)\n",
            k, n, tempkm, tempnn, tempkmin, ib,
            A /* dataA(k,k) */, ldak,
            T /* dataT(k,k) */, descT.mb,
            C /* dataQ(k,n) */, ldqk,
            p_elem_A, descT.nb );

        CORE_zunmqr(PlasmaLeft, PlasmaNoTrans, tempkm, tempnn, tempkmin, ib,
                    A /* dataA(k,k) */, ldak,
                    T /* dataT(k,k) */, descT.mb,
                    C /* dataQ(k,n) */, ldqk,
                    p_elem_A, descT.nb );

        dague_private_memory_push( p_work, p_elem_A );

           );
}
END

zunmqr_in_data_T1(k) [profile = off]
  /* Execution Space */
  k = 0 .. descA.nt-1

  /* Locality */
  : dataT(k,k)

  READ  T    <- dataT(k,k)                      [type = LITTLE_T]
             -> T zunmqr(k, 0 .. descQ.nt-1)    [type = LITTLE_T]

BODY
{
    /* nothing */
}
END

zunmqr_in_data_A0(k) [profile = off]
  /* Execution Space */
  k = 0 .. descA.nt-1

  /* Locality */
  : dataA(k,k)

  READ  A    <- dataA(k,k)                      [type = LOWER_TILE]
             -> A zunmqr(k, 0 .. (descQ.nt-1))  [type = LOWER_TILE]

BODY
{
    /* nothing */
}
END

ztsmqr(k, m, n)
  /* Execution Space */
  k = 0     .. inline_c %{ return dague_imin((descQ.mt-1),(descA.nt-1)); %}
  m = (k+1) .. (descQ.mt-1)
  n = 0     .. (descQ.nt-1)

  /* Locality */
  : dataQ(m,n)

  RW    A1   -> ( m == (k+1) ) ? C  zunmqr(k, n)
             -> ( m >  (k+1) ) ? A1 ztsmqr(k, m-1, n)
             <- ( m <  (descQ.mt-1) ) ? A1 ztsmqr(k, m+1, n)
             <- ( m == (descQ.mt-1) ) ? A1 ztsmqr_out_data_Q0(k, n)
  RW    A2   -> ( k == 0 ) ? dataQ(m,n)
             -> ( k >  0 ) ? A2 ztsmqr(k-1, m, n)
             <-  (k == (descA.nt-1)) ? dataQ(m,n)
             <- ((k <  (descA.nt-1)) & (m == (k+1))) ? C  zunmqr(k+1, n)
             <- ((k <  (descA.nt-1)) & (m >  (k+1))) ? A2 ztsmqr(k+1, m, n)
  READ  V    <- V ztsmqr_in_data_A1(k, m)
  READ  T    <- T ztsmqr_in_data_T2(k, m)  [type = LITTLE_T]

BODY
{
    int tempAkm  = (k == (descA.mt-1)) ? (descA.m - k * descA.mb) : descA.mb;
    int tempAkn  = (k == (descA.nt-1)) ? (descA.n - k * descA.nb) : descA.nb;
    int tempkmin = dague_imin( tempAkm, tempAkn );
    int tempmm   = (m == (descQ.mt-1)) ? (descQ.m - m * descQ.mb) : descQ.mb;
    int tempnn   = (n == (descQ.nt-1)) ? (descQ.n - n * descQ.nb) : descQ.nb;
    int ldam = BLKLDD( descA, m );
    int ldqk = BLKLDD( descQ, k );
    int ldqm = BLKLDD( descQ, m );
    int ldwork = ib;

    printlog("CORE_ztsmqr(%d, %d, %d)\n"
             "\t(side, trans, descQ.mb, tempnn, tempmm, tempnn, tempkmin, ib, Q(%d,%d)[%p], ldbk, Q(%d,%d)[%p], ldbm, A(%d,%d)[%p], ldam, T(%d,%d)[%p], descT.mb, p_elem_A, ldwork)\n",
             k, m, n, k, n, A1, m, n, A2, m, k, V, m, k, T);

    DRYRUN(
        void *p_elem_A = dague_private_memory_pop( p_work );

        fprintf(stderr,
                "CORE_ztsmqr[%d,%d,%d](Left, NoTrans, %d, %d, %d, %d, %d, %d,\n"
                "            %p, %d, %p, %d, %p, %d, %p, %d, %p, %d)\n",
                k, m, n, descQ.mb, tempnn, tempmm, tempnn, tempkmin, ib,
                A1, ldqk, A2, ldqm, V, ldam, T, descT.mb, p_elem_A, ldwork );
        
        CORE_ztsmqr(PlasmaLeft, PlasmaNoTrans, descQ.mb, tempnn,
                    tempmm, tempnn, tempkmin, ib,
                    A1 /* dataQ(k,n) */, ldqk,
                    A2 /* dataQ(m,n) */, ldqm,
                    V  /* dataA(m,k) */, ldam,
                    T  /* dataT(m,k) */, descT.mb,
                    p_elem_A, ldwork );

        dague_private_memory_push( p_work, p_elem_A );
           );
}
END

ztsmqr_in_data_T2(k, m) [profile = off]
  /* Execution Space */
  k = 0     .. inline_c %{ return dague_imin((descQ.mt-2),(descA.nt-1)); %}
  m = (k+1) .. (descQ.mt-1)

  /* Locality */
  : dataT(m,k)

  READ  T    <- dataT(m,k)                          [type = LITTLE_T]
             -> T ztsmqr(k, m, 0 .. (descQ.nt-1))   [type = LITTLE_T]

BODY
{
    /* nothing */
}
END

ztsmqr_in_data_A1(k, m) [profile = off]
  /* Execution Space */
  k = 0     .. inline_c %{ return dague_imin((descQ.mt-2),(descA.nt-1)); %}
  m = (k+1) .. (descQ.mt-1)

  /* Locality */
  : dataA(m,k)

  READ  V    <- dataA(m,k)
             -> V ztsmqr(k, m, 0 .. (descQ.nt-1))

BODY
{
    /* nothing */
}
END

ztsmqr_out_data_Q0(k, n) [profile = off]
  /* Execution Space */
  k = 0 .. inline_c %{ return dague_imin((descQ.mt-2),(descA.nt-1)); %}
  n = 0 .. (descQ.nt-1)

  /* Locality */
  : dataQ(k,n)

  READ  A1   -> A1 ztsmqr(k, descQ.mt-1, n)
             <- dataQ(k,n)

BODY
{
    /* nothing */
}
END

extern "C" %{
/*
 * Copyright (c) 2010-2013 The University of Tennessee and The University
 *                         of Tennessee Research Foundation. All rights
 *                         reserved.
 * Copyright (c) 2013      Inria. All rights reserved.
 * $COPYRIGHT
 *
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasma/lib/dplasmajdf.h"
#include "data_dist/matrix/matrix.h"
#include "data_dist/matrix/two_dim_rectangle_cyclic.h"
#include "dague/scheduling.h"

#define RECURSIVE_GEQRT

typedef struct cb_data_s {
    dague_execution_unit_t *context;
    dague_execution_context_t *task;
    dague_ddesc_t * A;
    dague_ddesc_t * B;
    dague_ddesc_t * C;
    void (*destruct)( dague_handle_t * );
} cb_data_t;

static int complete_recursive_dague_callback(dague_handle_t* dague_handle, void* cb_data)
{
    int rc = 0;
    cb_data_t* data = (cb_data_t*)cb_data;

    rc = __dague_complete_execution(data->context, data->task);

    if (data->A != NULL) free(data->A);
    if (data->B != NULL) free(data->B);
    if (data->C != NULL) free(data->C);

    data->destruct( dague_handle );
    free(data);
    return rc;
}

%}

dataA  [type = "dague_ddesc_t *"]
dataT  [type = "dague_ddesc_t *" aligned=dataA]
ib     [type = "int"]
p_work [type = "dague_memory_pool_t *" size = "(sizeof(dague_complex64_t)*ib*descT.nb)"]
p_tau  [type = "dague_memory_pool_t *" size = "(sizeof(dague_complex64_t)   *descT.nb)"]

descA  [type = "tiled_matrix_desc_t" hidden = on default = "*((tiled_matrix_desc_t*)dataA)"]
descT  [type = "tiled_matrix_desc_t" hidden = on default = "*((tiled_matrix_desc_t*)dataT)"]

zgeqrt_typechange(k)  [profile = off]
  /* Execution space */
  k = 0 .. inline_c %{ return dplasma_imin((descA.nt-1),(descA.mt-1)); %}

  : dataA(k,k)

RW A <- A zgeqrt(k)
     -> (k < (descA.nt-1)) ? A zunmqr(k, (k+1)..(descA.nt-1)) [type = LOWER_TILE]
     -> dataA(k, k)                                           [type = LOWER_TILE]

BODY
{
    /* Nothing */
}
END

zgeqrt(k)
  /* Execution space */
  k = 0 .. inline_c %{ return dplasma_imin((descA.nt-1),(descA.mt-1)); %}

  : dataA(k,k)

  RW    A <- (0 == k) ? dataA(k, k) : A2 ztsmqr(k-1, k, k)
          -> (descA.mt==(k+1)) ? dataA(k,k)                      [type = UPPER_TILE]
          -> (descA.mt>=(k+2)) ? A1 ztsqrt(k, k+1)               [type = UPPER_TILE]
          -> A zgeqrt_typechange(k)

  RW    T <- dataT(k, k)                                         [type = LITTLE_T]
          -> dataT(k, k)                                         [type = LITTLE_T]
          -> (descA.nt-1 > k) ? T zunmqr(k, (k+1)..(descA.nt-1)) [type = LITTLE_T]

  /* Priority */
  ;(descA.nt-k)*(descA.nt-k)*(descA.nt-k)

BODY
{
    int tempkm = ((k)==(descA.mt-1)) ? (descA.m-(k*descA.mb)) : descA.mb;
    int tempkn = ((k)==(descA.nt-1)) ? (descA.n-(k*descA.nb)) : descA.nb;
    int ldak = BLKLDD( descA, k );

    printlog("CORE_zgeqrt(%d)\n"
             "\t(tempkm, tempkn, ib, A(%d,%d)[%p], ldak, T(%d,%d)[%p], descT.mb, p_elem_A, p_elem_B)\n",
             k, k, k, A, k, k, T);

#if defined(RECURSIVE_GEQRT)

    two_dim_block_cyclic_t *small_descA;
    two_dim_block_cyclic_t *small_descT;
    dague_handle_t *dague_zgeqrt;
    cb_data_t      *cbdata_zgeqrt;

    small_descA = (two_dim_block_cyclic_t *) malloc(sizeof(two_dim_block_cyclic_t));
    small_descT = (two_dim_block_cyclic_t *) malloc(sizeof(two_dim_block_cyclic_t));
    
    two_dim_block_cyclic_init(small_descA,
                              matrix_ComplexDouble, matrix_Lapack,
                              1, context->virtual_process->dague_context->my_rank,
                              descA.mb, ib, ldak, tempkn,
                              0, 0, tempkm, tempkn, 1, 1, 1);
    two_dim_block_cyclic_init(small_descT,
                              matrix_ComplexDouble, matrix_Lapack,
                              1, context->virtual_process->dague_context->my_rank,
                              ib, ib, descT.mb, tempkn,
                              0, 0, ib, tempkn, 1, 1, 1);
    small_descA->mat = A;
    small_descT->mat = T;

    /* Dague_object */
    dague_zgeqrt = dplasma_zgeqrt_New((tiled_matrix_desc_t *)small_descA, (tiled_matrix_desc_t *)small_descT );
    dague_zgeqrt->devices_mask = 1;

    /* Callback */
    cbdata_zgeqrt = (cb_data_t *) malloc(sizeof(cb_data_t));
    cbdata_zgeqrt->context = context;
    cbdata_zgeqrt->task    = this_task;
    cbdata_zgeqrt->A       = (dague_ddesc_t *)small_descA;
    cbdata_zgeqrt->B       = (dague_ddesc_t *)small_descT;
    cbdata_zgeqrt->C       = NULL;
    cbdata_zgeqrt->destruct= dplasma_zgeqrt_Destruct;
    dague_set_complete_callback(dague_zgeqrt,
                                complete_recursive_dague_callback,
                                (void *)cbdata_zgeqrt);

    dague_enqueue(context->virtual_process->dague_context, dague_zgeqrt);
    return -1;

#else /* !defined(RECURSIVE_GEQRT) */

#if !defined(DAGUE_DRY_RUN)

    void *p_elem_A = dague_private_memory_pop( p_tau );
    void *p_elem_B = dague_private_memory_pop( p_work );

    CORE_zgeqrt(tempkm, tempkn, ib,
                A /* dataA(k,k) */, ldak,
                T /* dataT(k,k) */, descT.mb,
                p_elem_A, p_elem_B );

    dague_private_memory_push( p_tau,  p_elem_A );
    dague_private_memory_push( p_work, p_elem_B );

#endif  /* !defined(DAGUE_DRY_RUN) */

#endif /* end of defined(RECURSIVE_GEQRT) */
}
END


zunmqr(k,n)
  /* Execution space */
  k = 0   .. inline_c %{ return dplasma_imin((descA.nt-2),(descA.mt-1)); %}
  n = k+1 .. descA.nt-1

  : dataA(k,n)

  READ  A <- A zgeqrt_typechange(k)                        [type = LOWER_TILE]
  READ  T <- T zgeqrt(k)                                   [type = LITTLE_T]
  RW    C <- (k == 0) ? dataA(k, n) : A2 ztsmqr(k-1, k, n)
          -> (k == (descA.mt-1)) ? dataA(k, n)
          -> (k <  (descA.mt-1)) ? A1 ztsmqr(k, k+1, n)

BODY
{
    int tempkm = ((k)==(descA.mt-1)) ? (descA.m-(k*descA.mb)) : descA.mb;
    int tempnn = ((n)==(descA.nt-1)) ? (descA.n-(n*descA.nb)) : descA.nb;
    int ldak = BLKLDD( descA, k );

    printlog("CORE_zunmqr(%d, %d)\n"
             "\t(PlasmaLeft, PlasmaConjTrans, tempkm, tempnn, tempkm, ib, A(%d,%d)[%p], ldak, T(%d,%d)[%p], descT.mb, A(%d,%d)[%p], ldak, p_elem_A, descT.nb)\n",
             k, n, k, k, A, k, k, T, k, n, C);

#if !defined(DAGUE_DRY_RUN)

    void *p_elem_A = dague_private_memory_pop( p_work );

    CORE_zunmqr(PlasmaLeft, PlasmaConjTrans,
                tempkm, tempnn, tempkm, ib,
                A /* dataA(k,k) */, ldak,
                T /* dataT(k,k) */, descT.mb,
                C /* dataA(k,n) */, ldak,
                p_elem_A, descT.nb );

    dague_private_memory_push( p_work, p_elem_A );

#endif  /* !defined(DAGUE_DRY_RUN) */
}
END


ztsqrt_out_Ak(k) [profile = off]
  k = 0..( (descA.mt <= descA.nt) ? descA.mt-2 : descA.nt-1 )

  : dataA(k, k)

  RW A1 <- A1 ztsqrt(k, descA.mt-1) [type = UPPER_TILE]
        -> dataA(k, k)              [type = UPPER_TILE]
BODY
{
    /* nothing */
}
END

ztsqrt(k,m)
  /* Execution space */
  k = 0   .. inline_c %{ return dplasma_imin((descA.nt-1),(descA.mt-2)); %}
  m = k+1 .. descA.mt-1

  : dataA(m, k)

  RW   A1 <- ( m == (k+1)       ) ? A zgeqrt(m-1) : A1 ztsqrt(k, m-1)       [type = UPPER_TILE]
          -> ( m == (descA.mt-1)) ? A1 ztsqrt_out_Ak(k) : A1 ztsqrt(k, m+1) [type = UPPER_TILE]

  RW   A2 <- (k == 0) ? dataA(m, k) : A2 ztsmqr(k-1, m, k)
          -> dataA(m, k)
          -> (k < (descA.nt-1)) ? V ztsmqr(k, m, (k+1)..(descA.nt-1))

  RW   T  <- dataT(m, k)                                                    [type = LITTLE_T]
          -> dataT(m, k)                                                    [type = LITTLE_T]
          -> (k < (descA.nt-1)) ? T ztsmqr(k, m, (k+1)..(descA.nt-1))       [type = LITTLE_T]

    /* Priority */
; (descA.mt-k)*(descA.mt-k)*(descA.mt-k)

BODY
{
    int tempmm = ((m)==(descA.mt-1)) ? (descA.m-(m*descA.mb)) : descA.mb;
    int tempkn = ((k)==(descA.nt-1)) ? (descA.n-(k*descA.nb)) : descA.nb;
    int ldak = BLKLDD( descA, k );
    int ldam = BLKLDD( descA, m );

    printlog("CORE_ztsqrt(%d, %d)\n"
             "\t(tempmm, tempkn, ib, A(%d,%d)[%p], ldak, A(%d,%d)[%p], ldam, T(%d,%d)[%p], descT.mb, p_elem_A, p_elem_B)\n",
             k, m, k, k, T, m, k, A1, m, k, A2);

#if !defined(DAGUE_DRY_RUN)

    void *p_elem_A = dague_private_memory_pop( p_tau );
    void *p_elem_B = dague_private_memory_pop( p_work );

    CORE_ztsqrt(tempmm, tempkn, ib,
                A1 /* dataA(k,k) */, ldak,
                A2 /* dataA(m,k) */, ldam,
                T  /* dataT(m,k) */, descT.mb,
                p_elem_A, p_elem_B );

    dague_private_memory_push( p_tau,  p_elem_A );
    dague_private_memory_push( p_work, p_elem_B );

#endif  /* !defined(DAGUE_DRY_RUN) */
}
END


ztsmqr_out_A1(k, n) [profile = off]
  k = 0   .. inline_c %{ return dplasma_imin((descA.nt-2),(descA.mt-2)); %}
  n = k+1 .. descA.nt-1

  : dataA(k, n)

  RW A1 <- A1 ztsmqr(k, descA.mt-1, n)
       -> dataA(k, n)
BODY
{
    /* nothing */
}
END


ztsmqr(k,m,n)
  /* Execution space */
  k = 0     .. inline_c %{ return dplasma_imin((descA.mt-2),(descA.nt-2)); %}
  m = (k+1) .. (descA.mt-1)
  n = (k+1) .. (descA.nt-1)

  : dataA(m, n)

  RW   A1 <- ( (k+1) == m ) ? C zunmqr(m-1, n) : A1 ztsmqr(k, m-1, n)
          -> ( m == (descA.mt-1) ) ? A1 ztsmqr_out_A1(k, n) : A1 ztsmqr(k, m+1, n)

  RW   A2 <- (0==k) ? dataA(m, n) : A2 ztsmqr(k-1, m, n)
          -> (((k+1) == n) & ((k+1) == m)) ? A  zgeqrt(n)            /* Diagonal */
          -> (((k+1) == m) & (    n > m )) ? C  zunmqr(k+1, n)       /* Next row */
          -> (((k+1) == n) & (    m > n )) ? A2 ztsqrt(n, m)         /* Next column */
          -> (((k+1) <  n) & ((1+k) < m )) ? A2 ztsmqr(k+1, m, n)    /* trailing submatrix */

  READ V  <- A2 ztsqrt(k, m)
  READ T  <- T  ztsqrt(k, m)   [type = LITTLE_T]

    /* Priority */
; (descA.mt-k)*(descA.mt-n)*(descA.mt-n)

BODY
{
    int tempmm = ((m)==(descA.mt-1)) ? (descA.m-(m*descA.mb)) : descA.mb;
    int tempnn = ((n)==(descA.nt-1)) ? (descA.n-(n*descA.nb)) : descA.nb;
    int ldak = BLKLDD( descA, k );
    int ldam = BLKLDD( descA, m );
    int ldwork = ib;

    printlog("CORE_ztsmqr(%d, %d, %d)\n"
             "\t(PlasmaLeft, PlasmaConjTrans, descA.mb, tempnn, tempmm, tempnn, descA.nb, ib, A(%d,%d)[%p], ldak, A(%d,%d)[%p], ldam, A(%d,%d)[%p], ldam, T(%d,%d)[%p], descT.mb, p_elem_A, ldwork)\n",
             k, m, n, k, n, A1, m, n, A2, m, k, V, m, k, T);

#if !defined(DAGUE_DRY_RUN)
    void *p_elem_A = dague_private_memory_pop( p_work );

    CORE_ztsmqr(PlasmaLeft, PlasmaConjTrans,
                descA.mb, tempnn, tempmm, tempnn, descA.nb, ib,
                A1 /* dataA(k,n) */, ldak,
                A2 /* dataA(m,n) */, ldam,
                V  /* dataA(m,k) */, ldam,
                T  /* dataT(m,k) */, descT.mb,
                p_elem_A, ldwork );

    dague_private_memory_push( p_work, p_elem_A );

#endif  /* !defined(DAGUE_DRY_RUN) */
}
END

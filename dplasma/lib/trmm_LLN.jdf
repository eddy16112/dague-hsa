extern "C" %{
  /**
   * PLASMA include for defined and constants.
   */
  
#include <plasma.h>
#include <core_blas.h>
#include "dague.h"
#include "remote_dep.h"
#include "data_dist/data_distribution.h"
#include "dplasmatypes.h"

#if defined(DISTRIBUTED)
extern dague_arena_t CONTROL;
#endif

%}

/* Globals
 * GRIDrows, GRIDcols, NB, SIZE
 */
Amb  [int]
Anb  [int]
Amt  [int]
Ant  [int]
Am   [int]
An   [int]

Bmb  [int]
Bnb  [int]
Bmt  [int]
Bnt  [int]
Bm   [int]
Bn   [int]
     
side [int]
uplo [int]
trans[int]
diag [int]

alpha[TYPENAME]

/**************************************************
 *                      TRMM                      *
 **************************************************/
TRMM(m, n)

// Execution space
m = 0..Bmt-1
n = 0..Bnt-1

// Parallel partitioning
: B(m, n)

// Parameters
READ  A   <- A(m, m)
RW    B   <- B(m, n)
          -> (m == 0)     ? B(m, n)   : C GEMM(m, n, m-1)
READ  CTL <- (m == Bmt-1) ? CTL(m, n) : CTL1 GOK(m+1, n, m)              [CONTROL]

BODY
      CORE(
	   trmm, (
		  side, uplo, trans, diag,
		  m == Bmt-1 ? Bm - m * Bmb : Bmb,
		  n == Bnt-1 ? Bn - n * Bnb : Bnb,
		  alpha, A /*A(k, k)*/, Amb,
		         B /*B(k, n)*/, Bmb)
	   );

      printlog("thread %d trmm( %d, %d )\n"
	       "    ( %s, %s, %s, %s, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d)\n",
	       context->eu_id, m, n, "Left", "Upper", "[Conj]Trans", "[Non]Unit",
	       m == Bmt-1 ? Bm - m * Bmb : Bmb,
	       n == Bnt-1 ? Bn - n * Bnb : Bnb,
	       alpha, m, m, A /*A(k, k)*/, Amb,
	              m, n, B /*B(k, n)*/, Bmb);
END

/**************************************************
 *                      GEMM                      *
 **************************************************/
// Name
GEMM(m, n, k)

// Execution space
m = 0..Bmt-1
n = 0..Bnt-1
k = 0..m-1

// Parallel partitioning
: B(m, n)

// Parameters
READ  A   <- A(m, k)
READ  B   <- B(k, n) 
RW    C   <- (k == m-1) ? B TRMM(m, n) : C GEMM(m, n, k+1)
          -> (k == 0)   ? B(m, n)      : C GEMM(m, n, k-1)
RW    CTL <- CTL(k, n)                                          [CONTROL]
          -> CTL2 GOK(m, n, k)                                  [CONTROL]
BODY
      CORE(
	   gemm, (
		  trans, PlasmaNoTrans,
		  m == Bmt-1 ? Bm-m*Bmb : Bmb,
		  n == Bnt-1 ? Bn-n*Bnb : Bnb,
		  Amb,
		  alpha, A, Amb,
		         B, Bmb,
		  (TYPENAME)1.0, C, Bmb)
	   );

      printlog("thread %d gemm( %d, %d, %d )\n"
	       "    ( %s, %s, %d, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d, %f, B(%d,%d)[%p], %d)\n",
	       context->eu_id, m, n, k, "NoTrans", "NoTrans", 
	       m == Bmt-1 ? Bm-m*Bmb : Bmb,
	       n == Bnt-1 ? Bn-n*Bnb : Bnb,
	       Amb,
	       alpha, m, k, A, Bmb, k, n, B, Bmb,
	       (TYPENAME)1.0, m, n, C, Bmb);
END

/**************************************************
 *                      GOK                       *
 **************************************************/
// Name
GOK(m, n, k)

// Execution space
m = 0..Bmt-1
n = 0..Bnt-1
k = 0..m-1

// Parallel partitioning
: B(m, n)

// Parameters
RW    CTL1 <- (m == Bmt-1) ? CTL(m, n)      : CTL1 GOK(m+1, n, k)    [CONTROL]
           -> (m-1 == k)   ? CTL TRMM(k, n) : CTL1 GOK(m-1, n, k)    [CONTROL]
READ  CTL2 <- CTL GEMM(m, n, k)                                      [CONTROL]
BODY
      /* Fake task to guaranty the end of all gemm using B(m, n) before to start TRMM */
     printlog("thread %d gok( %d, %d, %d )\n", context->eu_id, m, n, k);
END

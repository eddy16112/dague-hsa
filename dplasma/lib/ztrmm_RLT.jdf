extern "C" %{
  /**
   * PLASMA include for defined and constants.
   *
   * @precisions normal z -> s d c
   *
   */
  
#include <plasma.h>
#include <core_blas.h>
#include "dague.h"
#include "remote_dep.h"
#include "data_distribution.h"
#include "dplasmatypes.h"

%}

/* Globals
 * GRIDrows, GRIDcols, NB, SIZE
 */
Amb  [int]
Anb  [int]
Amt  [int]
Ant  [int]
Am   [int]
An   [int]

Bmb  [int]
Bnb  [int]
Bmt  [int]
Bnt  [int]
Bm   [int]
Bn   [int]
     
side [int]
uplo [int]
trans[int]
diag [int]

alpha[Dague_Complex64_t]

/**************************************************
 *                      TRMM                      *
 **************************************************/
TRMM(m, n)

// Execution space
m = 0..Bmt-1
n = 0..Bnt-1

// Parallel partitioning
: B(m, n)

// Parameters
READ  A   <- A(n, n)
RW    B   <- B(m, n)
          -> (n == 0)     ? B(m, n)   : C GEMM(m, n, n-1)
READ  CTL <- (n == Bnt-1) ? CTL(m, n) : CTL1 GOK(m, n+1, n)              [CONTROL]

BODY
      DRYRUN(
	   CORE_ztrmm(
		  side, uplo, trans, diag,
		  m == Bmt-1 ? Bm - m * Bmb : Bmb,
		  n == Bnt-1 ? Bn - n * Bnb : Bnb,
		  alpha, A /*A(k, k)*/, Amb,
		         B /*B(k, n)*/, Bmb)
	   );

      printlog("thread %d trmm( %d, %d )\n"
	       "    ( %s, %s, %s, %s, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d)\n",
	       context->eu_id, m, n, "Right", "Upper", "NoTrans", "[Non]Unit",
	       m == Bmt-1 ? Bm - m * Bmb : Bmb,
	       n == Bnt-1 ? Bn - n * Bnb : Bnb,
	       alpha, n, n, A /*A(k, k)*/, Amb,
	              m, n, B /*B(k, n)*/, Bmb);
END

/**************************************************
 *                      GEMM                      *
 **************************************************/
// Name
GEMM(m, n, k)

// Execution space
m = 0..Bmt-1
n = 0..Bnt-1
k = 0..n-1

// Parallel partitioning
: B(m, n)

// Parameters
READ  A   <- B(m, k)
READ  B   <- A(n, k) 
RW    C   <- (k == n-1) ? B TRMM(m, n) : C GEMM(m, n, k+1)
          -> (k == 0)   ? B(m, n)      : C GEMM(m, n, k-1)
RW    CTL <- CTL(k, n)                                          [CONTROL]
          -> CTL2 GOK(m, n, k)                                  [CONTROL]
BODY
      DRYRUN(
	   CORE_zgemm(
		  PlasmaNoTrans, trans,
		  m == Bmt-1 ? Bm-m*Bmb : Bmb,
		  n == Bnt-1 ? Bn-n*Bnb : Bnb,
		  Bmb,
		  alpha, A, Bmb,
		         B, Amb,
		  (Dague_Complex64_t)1.0,  C, Bmb)
	   );
				       
      printlog("thread %d gemm( %d, %d, %d )\n"
	       "    ( %s, %s, %d, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d, %f, B(%d,%d)[%p], %d)\n",
	       context->eu_id, m, n, k, "NoTrans", "[Conj]Trans", 
	       m == Bmt-1 ? Bm-m*Bmb : Bmb,
	       n == Bnt-1 ? Bn-n*Bnb : Bnb,
	       Bmb,
	       alpha, m, k, A, Bmb,
	              n, k, B, Bmb,
	       (Dague_Complex64_t)1.0, C, Bmb);
END

/**************************************************
 *                      GOK                       *
 **************************************************/
// Name
GOK(m, n, k)

// Execution space
m = 0..Bmt-1
n = 0..Bnt-1
k = 0..n-1

// Parallel partitioning
: B(m, n)

// Parameters
RW    CTL1 <- (n   == Bnt-1) ? CTL(m, n)      : CTL1 GOK(m, n+1, k) [CONTROL]
           -> (n-1 == k    ) ? CTL TRMM(m, k) : CTL1 GOK(m, n-1, k) [CONTROL]
READ  CTL2 <- CTL GEMM(m, n, k)                                     [CONTROL]
BODY
      /* Fake task to guaranty the end of all gemm using B(m, n) before to start TRMM */
     printlog("thread %d gok( %d, %d, %d )\n", context->eu_id, m, n, k);
END

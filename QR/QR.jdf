GRIDrows = 1
GRIDcols = 1
// NB = 120
SIZE = 5
colRANK = 0
rowRANK = 0

/*****************************************
 9:11
*/
DGEQRT(k)

  // Execution space
  k = 0..SIZE-1

  // Parallel partitioning
  : k % GRIDrows == rowRANK
  : k % GRIDcols == colRANK

  IN RV1 <- (k==0) ? IN(k,k) : C2 DSSRFB(k-1,k,k)

  OUT RV1 -> (k==SIZE-1) ? OUT(k,k) : R DTSQRT(k,k+1)
  OUT T   -> T DLARFB(k, k+1..SIZE-1)

BODY
fake_dgeqrt(NB, RV1, T);

END

/*****************************************
 17:21
*/
DTSQRT(k,m)

  // Execution space
  k = 0..SIZE-2
  m = k+1..SIZE-1

  // Parallel partitioning
  : m % GRIDrows == rowRANK
  : k % GRIDcols == colRANK

  IN V2 <- (k==0) ? IN(m,k) : C2 DSSRFB(k-1,k,m)
  IN R  <- (m==k+1) ? RV1 DGEQRT(k) : R DTSQRT(k,m-1)

  OUT R  -> (m==SIZE-1) ?  V1 DLARFB(k,k+1..SIZE-1) : R DTSQRT(k,m+1)
  OUT V2 -> V2 DSSRFB(k,k+1..SIZE-1,m)
  OUT T  -> T DSSRFB(k,k+1..SIZE-1,m)

BODY
fake_dtsqrt(NB, R, T, V2);

END

/*****************************************
 28:31
*/
DLARFB(k,n)

  // Execution space
  k = 0..SIZE-2
  n = k+1..SIZE-1

  // Parallel partitioning
  : k % GRIDrows == rowRANK
  : k % GRIDcols == colRANK


  IN C1 <- (k==0) ? IN(k,n) : C2 DSSRFB(k-1,n,k)
  IN T  <-  T DGEQRT(k)
  IN V1 <- R DTSQRT(k, SIZE-1)

  OUT C1 -> C1 DSSRFB(k,n,k+1)

BODY
fake_dlarfb(NB, C1, T, V1);

END

/*****************************************
 37:41
*/
DSSRFB(k,n,m)

  // Execution space
  k = 0..SIZE-2
  n = k+1..SIZE-1
  m = k+1..SIZE-1

  // Parallel partitioning
  : m % GRIDrows == rowRANK
  : k % GRIDcols == colRANK


  IN C2 <- (k==0) ? IN(m,n) : C2 DSSRFB(k-1,n,m)
  IN V2 <- V2 DTSQRT(k,m)
  IN T  <- T DTSQRT(k,m)
  IN C1 <- (m==k+1) ? C1 DLARFB(k,n) : C1 DSSRFB(k,n,m-1)

  OUT C2 -> ((m==k+1) & (n==k+1)) ? RV1 DGEQRT(k+1) : OUT(1,1)
  OUT C2 -> ((m==k+1) & (n!=k+1))  ? C1 DLARFB(k+1,n) : OUT(1,1)
  OUT C2 -> ((m!=k+1) & (n==k+1)) ? V2 DTSQRT(k+1,m) : OUT(1,1)
  OUT C2 -> ((m!=k+1) & (n!=k+1))  ? C2 DSSRFB(k+1,n,m) : OUT(1,1)

  OUT C1 -> (m==SIZE-1) ? OUT(k,n) : C1 DSSRFB(k,n,m+1)

BODY
fake_dssrfb(NB, C1, C2, T, V2);

END

